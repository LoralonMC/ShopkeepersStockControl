plugins {
    id 'java'
    //id 'com.github.johnrengelman.shadow' version '8.1.1'  // Disabled due to Windows file locking
    id 'xyz.jpenilla.run-paper' version '2.3.1'
}

group = 'dev.oakheart'
version = '1.0.0'

repositories {
    mavenCentral()

    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }

    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }

    maven {
        name = 'codemc-repo'
        url = 'https://repo.codemc.io/repository/maven-public/'
    }

    maven {
        name = 'codemc-snapshots'
        url = 'https://repo.codemc.io/repository/maven-snapshots/'
    }

    maven {
        name = 'jitpack'
        url = 'https://jitpack.io'
    }

    maven {
        name = 'CodeMC'
        url = 'https://repo.codemc.io/repository/maven-public/'
    }
}

dependencies {
    // Paper API (Paper-only, not Spigot compatible)
    compileOnly 'io.papermc.paper:paper-api:1.21-R0.1-SNAPSHOT'

    // Shopkeepers API (local JAR - see README for download instructions)
    compileOnly files('libs/Shopkeepers-2.24.0.jar')

    // PacketEvents (use server's version instead of bundling)
    compileOnly 'com.github.retrooper:packetevents-spigot:2.10.0'

    // SQLite JDBC (shaded into plugin)
    implementation 'org.xerial:sqlite-jdbc:3.42.0.0'

    // bStats (metrics)
    implementation 'org.bstats:bstats-bukkit:3.1.0'

    // Note: MiniMessage is included in Paper API 1.21+, no separate dependency needed
}

tasks {
    runServer {
        minecraftVersion('1.21')
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

// Custom fat jar task (avoids Shadow plugin's Windows file locking issues)
task fatJar(type: Jar) {
    archiveBaseName.set('ShopkeepersStockControl')
    archiveVersion.set('')
    archiveClassifier.set('')

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    from sourceSets.main.output

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/DEPENDENCIES'
}

build.dependsOn fatJar
