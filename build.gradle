plugins {
    id 'java'
    id("com.gradleup.shadow") version "9.3.1"
}

group = 'dev.oakheart'
version = '1.2.0'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "codemc-repo"
        url = "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        name = "jitpack"
        url = "https://jitpack.io"
    }
    maven {
        name = "placeholderapi"
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
    maven {
        name = "shopkeepers-repo"
        url = "https://raw.githubusercontent.com/Shopkeepers/Repository/main/releases/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT")
    compileOnly("com.nisovin.shopkeepers:ShopkeepersAPI:2.24.0")
    compileOnly("com.github.retrooper:packetevents-spigot:2.10.0")
    compileOnly("me.clip:placeholderapi:2.11.6")
    compileOnly("org.xerial:sqlite-jdbc:3.42.0.0")
    implementation("org.bstats:bstats-bukkit:3.1.0")
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

compileJava {
    options.encoding = 'UTF-8'
    options.release = 21
    options.compilerArgs += ['-Xlint:deprecation', '-Xlint:unchecked']
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching(['plugin.yml', 'paper-plugin.yml']) {
        expand props
    }
}

shadowJar {
    archiveBaseName.set('ShopkeepersStockControl')
    archiveClassifier.set('')
    relocate 'org.bstats', 'dev.oakheart.stockcontrol.bstats'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude 'META-INF/DEPENDENCIES'
}

build.dependsOn shadowJar
